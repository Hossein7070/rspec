The <code>FOR</code> loop at first seems like a convenient way of iterating over the elements of a collection, but doing so will raise a <code>VALUE_ERROR</code> exception if the collection is empty. Looping instead from 1 to <code>COUNT</code> doesn't work either if the collection is sparse; that leads to a <code>ORA-01403: no data found</code> error. 

Instead, a <code>WHILE</code> loop should be used.


== Noncompliant Code Example

----
DECLARE
  TYPE fooType IS TABLE OF VARCHAR2(42);
  foo fooType := new fooType('Strawberry', 'Apple', 'Banana');
BEGIN
  foo.DELETE(2);                                -- The collection is now sparse

  FOR i IN 1 .. foo.COUNT                       -- Noncompliant - leads to ORA-01403: no data found
  LOOP
    DBMS_OUTPUT.PUT_LINE(i || ' = ' || foo(i));
  END LOOP;
END;
/
----


== Compliant Solution

----
DECLARE
  TYPE fooType IS TABLE OF VARCHAR2(42);
  foo fooType := new fooType('Strawberry', 'Apple', 'Banana');
  i PLS_INTEGER;
BEGIN
  foo.DELETE(2);                                -- The collection is now sparse

  i := foo.FIRST;

  WHILE (i IS NOT NULL)                         -- Compliant - works as expected
  LOOP
    DBMS_OUTPUT.PUT_LINE(i || ' = ' || foo(i));
    i := foo.NEXT(i);
  END LOOP;
END;
/
----

